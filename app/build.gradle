apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply from: '../coverage.gradle'
apply from: '../google-services.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.github.openwebnet"
        // SDK Tools, Revision 21 (November 2012) - Android 5.0
        minSdkVersion 21
        targetSdkVersion 28
        versionCode currentVersionCode
        versionName currentVersionName
        multiDexEnabled true
        javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
        lintConfig file('lint.xml')
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    // Robolectric issue
    useLibrary 'org.apache.http.legacy'

    buildTypes {
        // store
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            it.buildConfigField 'String', 'IAB_KEY', project.hasProperty('IAB_KEY') ? project.getProperty('IAB_KEY') : '\"\"'
        }
        // local
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            it.buildConfigField 'String', 'IAB_KEY', '\"\"'
        }
        // travis
        dev {
            it.buildConfigField 'String', 'IAB_KEY', '\"\"'
            testCoverageEnabled true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation 'org.robolectric:shadows-support-v4:3.4-rc2'
    testImplementation 'org.powermock:powermock-core:1.6.6'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.6'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.6.6'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.6'
    testImplementation 'org.powermock:powermock-classloading-xstream:1.6.6'
    testCompileOnly('com.google.dagger:dagger-compiler:2.18') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    testCompileOnly('com.jakewharton:butterknife-compiler:9.0.0-rc1') {
        exclude group: 'com.google.guava', module: 'guava'
    }

    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'

    implementation 'com.firebaseui:firebase-ui-auth:4.3.0'
    implementation 'com.firebaseui:firebase-ui-firestore:4.3.0'

    implementation 'com.github.gabrielemariotti.changeloglib:changelog:2.1.0'
    implementation 'com.github.scottyab:secure-preferences:v0.1.6'

    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    implementation('com.github.openwebnet:rx-openwebnet:1.6.1') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    implementation 'com.github.niqdev:mjpeg-view:1.4.0'

    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.noveogroup.android:android-logger:1.3.6'
    implementation 'com.google.guava:guava:26.0-android'

    implementation 'com.annimon:stream:1.2.1'
    implementation 'org.threeten:threetenbp:1.3.7'
    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava-computation-expressions:0.21.0'

    implementation('com.jakewharton:butterknife:9.0.0-rc1') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    annotationProcessor('com.jakewharton:butterknife-compiler:9.0.0-rc1') {
        exclude group: 'com.google.guava', module: 'guava'
    }

    implementation('com.google.dagger:dagger:2.18') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    annotationProcessor('com.google.dagger:dagger-compiler:2.18') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compileOnly 'javax.annotation:jsr250-api:1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

task hello(dependsOn: build) {
    println ''
    println '         _              _          _            _            _     _          _ '
    println '        /\\ \\     _     /\\ \\       /\\ \\         /\\ \\         /\\ \\  /\\ \\    _ / /\\'
    println '       /  \\ \\   /\\_\\   \\ \\ \\     /  \\ \\       /  \\ \\____   /  \\ \\ \\ \\ \\  /_/ / /'
    println '      / /\\ \\ \\_/ / /   /\\ \\_\\   / /\\ \\ \\     / /\\ \\_____\\ / /\\ \\ \\ \\ \\ \\ \\___\\/ '
    println '     / / /\\ \\___/ /   / /\\/_/  / / /\\ \\ \\   / / /\\/___  // / /\\ \\_\\/ / /  \\ \\ \\ '
    println '    / / /  \\/____/   / / /    / / /  \\ \\_\\ / / /   / / // /_/_ \\/_/\\ \\ \\   \\_\\ \\'
    println '   / / /    / / /   / / /    / / / _ / / // / /   / / // /____/\\    \\ \\ \\  / / /'
    println '  / / /    / / /   / / /    / / / /\\ \\/ // / /   / / // /\\____\\/     \\ \\ \\/ / / '
    println ' / / /    / / /___/ / /__  / / /__\\ \\ \\/ \\ \\ \\__/ / // / /______      \\ \\ \\/ /  '
    println '/ / /    / / //\\__\\/_/___\\/ / /____\\ \\ \\  \\ \\___\\/ // / /_______\\      \\ \\  /   '
    println '\\/_/     \\/_/ \\/_________/\\/________\\_\\/   \\/_____/ \\/__________/       \\_\\/    '
    println ':see github.com/openwebnet/openwebnet-android\n'

    println 'current directory is: ' + projectDir
    println 'JAVA_HOME: ' + System.getenv('JAVA_HOME')
}
